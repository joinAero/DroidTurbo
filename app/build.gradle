apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def getGitBranch() {
    def gitBranch = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'symbolic-ref', '--short', '-q', 'HEAD'
        standardOutput = gitBranch
        // ignore error output as we might not be on a branch
        ignoreExitValue = true
    }
    return gitBranch.toString().trim();
}

def getGitHash() {
    def gitHash = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = gitHash
        // ignore error output as we might not be on a branch
        ignoreExitValue = true
    }
    return gitHash.toString().trim();
}

android {
    compileSdkVersion versionCompileSdk
    buildToolsVersion versionBuildTools

    // Apache HTTP Client Removal - Android 6.0 Changes:
    // http://developer.android.com/about/versions/marshmallow/android-6.0-changes.html#behavior-apache-http-client
    //useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "cc.eevee.turbo"
        minSdkVersion versionMinSdk
        targetSdkVersion versionTargetSdk
        versionCode 1
        versionName "1.0.0_" + gitHash
        // Apps Over 65K Methods: http://developer.android.com/tools/building/multidex.html
        //multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        ndk {
            abiFilters ndkABIs
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
    compileOptions {
        // Gradle Retrolambda Plugin
        sourceCompatibility javaSourceCompatibility
        targetCompatibility javaTargetCompatibility
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
    }

    // Configure APK Splits: https://developer.android.com/studio/build/configure-apk-splits.html
    /*splits {
        // Configures screen ABI split settings.
        abi {
            // Enables ABI APK splits.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86, armeabi-v7a, and mips.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk true
        }
    }*/

    //project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apkName = 'DroidTurbo'
            if (variant.productFlavors.size() > 0) {
                apkName += '-' + variant.productFlavors[0].name
            }
            def branch = gitBranch
            if (branch != 'master') {
                apkName += '-' + branch
            }
            def version = variant.versionName
            def build = variant.buildType.name
            if (build == 'release') {
                def date = new Date().format('MMddHHmm')
                //def date = new Date().format('yyyyMMdd_HHmmssSSS')
                build += '_' + date
            }
            apkName += '-v' + version + '-' + build + '.apk'
            output.outputFile = new File(output.outputFile.parent, apkName)

            // assign different version code for each output
            /*
            def abi = project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0)
            output.versionCodeOverride = abi * 1000000 + android.defaultConfig.versionCode
            */
        }
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile compiles.supportAppCompat
    compile compiles.supportCardView
    compile compiles.supportDesgin
    compile compiles.supportConstraint

    // simplify

    compile compiles.butterknife
    apt apts.butterknifeCompiler

    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile compiles.guava

    // behavior

    compile compiles.rxandroid
    compile compiles.rxjava

    // view & widget

    compile compiles.gifdrawable

    // dependent libraries

    //compile project(':libgldraw')
    debugCompile project(path: ':libgldraw', configuration: 'debug')
    releaseCompile project(path: ':libgldraw', configuration: 'release')

    //compile project(':libopencv')
    debugCompile project(path: ':libopencv', configuration: 'debug')
    releaseCompile project(path: ':libopencv', configuration: 'release')

    // test

    testCompile testCompiles.junit
    testCompile testCompiles.assertj
    testCompile testCompiles.mockito

    // android test

    androidTestCompile compiles.supportAnnotations
    androidTestCompile androidTestCompiles.supportTestRunner
    androidTestCompile androidTestCompiles.supportTestRules
    androidTestCompile(androidTestCompiles.supportTestEspresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile androidTestCompiles.supportTestUiautomator

    androidTestCompile androidTestCompiles.assertjAndroid
    androidTestCompile androidTestCompiles.assertjAndroidSupport
    androidTestCompile androidTestCompiles.assertjAndroidAppCompat
    androidTestCompile androidTestCompiles.assertjAndroidDesign
    androidTestCompile androidTestCompiles.assertjAndroidCardView
    androidTestCompile androidTestCompiles.assertjAndroidRecyclerView
}

// Reference:
// * Android Plugin for Gradle: http://developer.android.com/tools/building/plugin-for-gradle.html
// * Gradle Plugin User Guide: http://tools.android.com/tech-docs/new-build-system/user-guide
