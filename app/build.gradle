buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        // android-apt: https://bitbucket.org/hvisser/android-apt
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        // Gradle Retrolambda: https://github.com/evant/gradle-retrolambda
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def getGitBranch() {
    def gitBranch = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'symbolic-ref', '--short', '-q', 'HEAD'
        standardOutput = gitBranch
        // ignore error output as we might not be on a branch
        ignoreExitValue = true
    }
    return gitBranch.toString().trim();
}

def getGitHash() {
    def gitHash = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = gitHash
        // ignore error output as we might not be on a branch
        ignoreExitValue = true
    }
    return gitHash.toString().trim();
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    // Apache HTTP Client Removal - Android 6.0 Changes:
    // http://developer.android.com/about/versions/marshmallow/android-6.0-changes.html#behavior-apache-http-client
    //useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "cc.eevee.turbo"
        minSdkVersion 18
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0_" + gitHash
        // Apps Over 65K Methods: http://developer.android.com/tools/building/multidex.html
        //multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Gradle Retrolambda Plugin
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apkName = 'DroidTurbo'
            if (variant.productFlavors.size() > 0) {
                apkName += '-' + variant.productFlavors[0].name
            }
            def branch = gitBranch
            if (branch != 'master') {
                apkName += '-' + branch
            }
            def version = variant.versionName
            def build = variant.buildType.name
            def date = new Date().format('MMddHHmm')
            //def date = new Date().format('yyyyMMdd_HHmmssSSS')
            apkName += '-v' + version + '-' + build + '_' + date + '.apk'
            output.outputFile = new File(output.outputFile.parent, apkName)
        }
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    //compile 'com.android.support:multidex:1.0.1'

    // simplify

    // Butter Knife: https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // behavior

    // RxAndroid: https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.3'

    // view & widget

    // android-gif-drawable: https://github.com/koral--/android-gif-drawable
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.3'

    // using in demos

    // MaterialDrawer: https://github.com/mikepenz/MaterialDrawer
    //compile('com.mikepenz:materialdrawer:5.8.1@aar') { transitive = true }

    // dependent libraries

    compile project(':libgldraw')

    // test

    testCompile 'junit:junit:4.12'
    // AssertJ: https://github.com/joel-costigliola/assertj-core
    testCompile 'org.assertj:assertj-core:2.5.0'
    // Mockito: https://github.com/mockito/mockito
    testCompile 'org.mockito:mockito-core:2.2.5'

    // android test
    // Testing Support Library: https://developer.android.com/topic/libraries/testing-support-library/index.html
    // android-unit-testing-tutorial: https://github.com/ChrisZou/android-unit-testing-tutorial

    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    // AssertJ Android: https://github.com/square/assertj-android
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile 'com.squareup.assertj:assertj-android-support-v4:1.1.1@aar'
    //androidTestCompile 'com.squareup.assertj:assertj-android-play-services:1.1.1@aar'
    androidTestCompile 'com.squareup.assertj:assertj-android-appcompat-v7:1.1.1@aar'
    androidTestCompile 'com.squareup.assertj:assertj-android-design:1.1.1@aar'
    //androidTestCompile 'com.squareup.assertj:assertj-android-mediarouter-v7:1.1.1@aar'
    //androidTestCompile 'com.squareup.assertj:assertj-android-gridlayout-v7:1.1.1@aar'
    androidTestCompile 'com.squareup.assertj:assertj-android-cardview-v7:1.1.1@aar'
    androidTestCompile 'com.squareup.assertj:assertj-android-recyclerview-v7:1.1.1@aar'
    //androidTestCompile 'com.squareup.assertj:assertj-android-palette-v7:1.1.1@aar'
}

// Reference:
// * Android Plugin for Gradle: http://developer.android.com/tools/building/plugin-for-gradle.html
// * Gradle Plugin User Guide: http://tools.android.com/tech-docs/new-build-system/user-guide
